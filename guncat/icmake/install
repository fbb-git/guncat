string setWhere(string where, string defaultWhere)
{
    if (where == "")
        where = defaultWhere;

    md(where);
    return where;
}

void install(string what, string where)
{
    string path;                // the path of the binary program
    string target;
    list pathElements;

    if (what == "program")
    {
        if (where == "")        // `where' is the path to the program
        {
            target = BINARY;    // by default BINARY
            path = get_path(target);
        }
        else
        {
            target = where;     // a specific destination was specified
            if (sizeof(makelist(O_DIR, target)) == 0)   // a filename?
                path = get_path(target);                // get the directory
            else
            {
                printf("build install program PATH: PATH must be the "
                        "full path to a program\n");
                exit(0);
            }
        }
        md(path);

        printf("  INSTALLING `", target, "'\n");

        run("icmbuild install program " + target);
        log(target);
        
        writeLog();
        exit(0);
    }

    if (what == "man")
    {
        where = setWhere(where, MAN);
        target = where + "/" PROJECT ".1.gz";
        printf("  INSTALLING `", target, "'\n");
        run("gzip -9 < tmp/man/" PROJECT ".1 > " + target);
        log(target);

        writeLog();
        exit(0);
    }

    if (what == "std")
    {
        where = setWhere(where, STD);

        target = where + "/changelog.gz";
        printf("  INSTALLING `", target, "'\n");
        run("gzip -9 < changelog > " + target);
        log(target);

        target = where + "/" + PROJECT ".1.html";
        printf("  INSTALLING `", target, "'\n");
        run("cp tmp/manhtml/" PROJECT ".1.html " + where);
        log(target);

        target = where + "/README";
        printf("  INSTALLING `", target, "'\n");
        run("cp README " + where);
        log(target);

        writeLog();
        exit(0);
    }

    exit(0);
}




